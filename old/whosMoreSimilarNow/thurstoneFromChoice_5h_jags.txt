# choice probabilities for "which pair is more similar" task
data{
  pi = 3.1415
}
model{
  for (g in 1:2){
    mu[1, g] ~ dunif(0, 1e-6)
    mu[2, g] ~ dunif(0, pi/2)
    for (j in 3:nStimuli){
      mu[j, g] ~ dunif(0, pi)
    }    
    sigma[g] ~ dunif(0, pi)
  }
  for (i in 1:nParticipants){
    z[i] ~ dcat(c(phi, 1-phi))
  }
  phi ~ dunif(0, 1)
  for (t in 1:nTrials){
    # raw mental samples
    xA[t] ~ dnorm(mu[a[t], z[p[t]]], 1/sigma[z[p[t]]]^2)
    xB[t] ~ dnorm(mu[b[t], z[p[t]]], 1/sigma[z[p[t]]]^2)
    xC[t] ~ dnorm(mu[c[t], z[p[t]]], 1/sigma[z[p[t]]]^2)
    xD[t] ~ dnorm(mu[d[t], z[p[t]]], 1/sigma[z[p[t]]]^2)
    # distances allowing for directionless orientations
    dAB[t] = min(abs(xA[t]-xB[t]), pi-abs(xA[t]-xB[t]))
    dCD[t] = min(abs(xC[t]-xD[t]), pi-abs(xC[t]-xD[t]))
    # response
    y[t] ~ dinterval(dAB[t] - dCD[t], 0)

    # posterior predictive
    xAp[t] ~ dnorm(mu[a[t], z[p[t]]], 1/sigma[z[p[t]]]^2)
    xBp[t] ~ dnorm(mu[b[t], z[p[t]]], 1/sigma[z[p[t]]]^2)
    xCp[t] ~ dnorm(mu[c[t], z[p[t]]], 1/sigma[z[p[t]]]^2)
    xDp[t] ~ dnorm(mu[d[t], z[p[t]]], 1/sigma[z[p[t]]]^2)
    # distances allowing for directionless orientations
    dABp[t] = min(abs(xAp[t]-xBp[t]), pi-abs(xAp[t]-xBp[t]))
    dCDp[t] = min(abs(xCp[t]-xDp[t]), pi-abs(xCp[t]-xDp[t]))
    # response
    yp[t] ~ dinterval(dABp[t] - dCDp[t], 0)
  }
}